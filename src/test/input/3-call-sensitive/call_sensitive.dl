// declarations
.input AssignmentFromConst
.input AssignmentFromVar
.input AssignmentFromCall
.input FormalArg
.input FunctionCallInfo
.input ActualConstArg
.input ActualVarArg
.input ReturnConst
.input ActualVarArg
.input ReturnConst
.input ReturnVar

.output PointsTo
.output CallPointsTo
.output ReachedCall

// >> Assignments <<
.decl AssignmentFromConst(v: symbol, const: symbol, method: symbol)
.decl AssignmentFromVar(v1: symbol, v2: symbol)
.decl AssignmentFromCall(v: symbol, call: symbol)

// >> Fucntions <<
.decl FormalArg(method: symbol, i: symbol, arg: symbol)
.decl FunctionCallInfo(inMethod: symbol, call: symbol, method: symbol)

.decl ActualConstArg(call: symbol, i: symbol, const: symbol)
.decl ActualVarArg(call: symbol, i: symbol, var: symbol)

.decl ReturnConst(method: symbol, const: symbol)
.decl ReturnVar(method: symbol, var: symbol)

// Output relations
.decl CallGraph(callerMethodCtx: symbol, callerMethod: symbol, call: symbol, method: symbol)
.output CallGraph

.decl PointsTo(var: symbol, const: symbol, ctx: symbol)
.decl CallPointsTo(callerMethodCtx: symbol, callerMethod: symbol, call: symbol, const: symbol)

.decl ReachedCall(ctx: symbol, method: symbol)
ReachedCall("global", "main").

// I. Построение графа вызова функций
ReachedCall(call, method),
CallGraph(callerMethodCtx, callerMethod, call, method) :-
    ReachedCall(callerMethodCtx, callerMethod),
    FunctionCallInfo(callerMethod, call, method).

// II. Базовый PointsTo.
PointsTo(var, const, ctx) :- AssignmentFromConst(var, const, method), ReachedCall(ctx, method).
PointsTo(var, const, ctx) :- AssignmentFromVar(var, y), PointsTo(y, const, ctx).

// III. Проталкивание аргументов в функцию
PointsTo(funArg, const, call) :-
    ReachedCall(call, method),
    ActualConstArg(call, i, const),
    FormalArg(method, i, funArg).

PointsTo(funArg, const, call) :-
    CallGraph(callerCtx, _, call, method),
    FormalArg(method, i, funArg),
    ActualVarArg(call, i, var),
    PointsTo(var, const, callerCtx).

// IV. Rules for propogating return values
CallPointsTo(callerMethodCtx, callerMethod, call, const) :-
    CallGraph(callerMethodCtx, callerMethod, call, method),
    ReturnConst(method, const).

CallPointsTo(callerMethodCtx, callerMethod, call, const) :-
    CallGraph(callerMethodCtx, callerMethod, call, method),
    ReturnVar(method, var),
    PointsTo(var, const, call).

PointsTo(var, const, callerMethodCtx) :-
    AssignmentFromCall(var, call),
    CallPointsTo(callerMethodCtx, _, call, const).
